;=======================================================================================
; Opera2IE.kxl
;=======================================================================================

   BREAK ON
   $Result = SetOption ("WrapAtEOL", "ON")

;=======================================================================================
; Uses
;=======================================================================================
   Call $KxlDir+"\UDF\LUStrings.kxl"
   Call $KxlDir+"\UDF\LUFile.kxl"
   Call $KxlDir+"\UDF\LUWord.kxl"
   Call $KxlDir+"\UDF\LUDecode.kxl"

   $Item = ""
   $NextItem = ""
   $ID = ""
   $NAME = ""
   $URL = ""
   $Level = 1

   Main($OperaFile,$IEDir)

;=======================================================================================
; 
;=======================================================================================
function ReadItem($Handle)
;begin
   $ID = ""
   $NAME = ""
   $URL = ""
   $Item = $NextItem
   $NextItem = Trim(ReadLine ($Handle))
   while (@ERROR = 0) and ((Substr($NextItem,1,1) <> "#") and (Substr($NextItem,1,1) <> "-"))
      $NextItem = UTF8toWin($NextItem)
      $TAG   = Trim(ExtractWord(1, $NextItem, "="))
      $VALUE = Trim(ExtractWord(2, $NextItem, "="))
      select
         case UCASE($TAG) = "ID"
            $ID = $VALUE
         case UCASE($TAG) = "URL"
            $URL = $VALUE
         case UCASE($TAG) = "NAME"
            $NAME = $VALUE
      endselect
      $NextItem = Trim(ReadLine ($Handle))
   loop
   $ReadItem = @ERROR
endfunction

;---------------------------------------------------------------
;
;---------------------------------------------------------------
function CreateFileURL($FOLDERName)
;begin
   $FileName = $FOLDERName+"\"+$NAME+".url"
   $HandleURL = FreeFileHandle
   if Open ($HandleURL, $FileName, 1+4) = 0
      $Res = WriteLine ($HandleURL, "[DEFAULT]"+@CRLF)
      $Res = WriteLine ($HandleURL, "BASEURL="+$URL+@CRLF)
      $Res = WriteLine ($HandleURL, "[InternetShortcut]"+@CRLF)
      $Res = WriteLine ($HandleURL, "URL="+$URL+@CRLF)
      $Res = Close ($HandleURL)
   endif
endfunction

;---------------------------------------------------------------
;
;---------------------------------------------------------------
function ScanFolder($Handle, $FOLDERName)
;begin
   $EOF = ReadItem($Handle)
   while ($EOF = 0)
      if $NAME
         ? AddChar (" ", "", ($Level-1)*3)  $Item "=" $NAME
      endif
      if UCASE($Item) = "#FOLDER"
         $LFOLDERName = $FOLDERName+"\"+$NAME
         MD $LFOLDERName
         $Level = $Level + 1
         ScanFolder($Handle, $LFOLDERName)
      endif      
      if UCASE($Item) = "-"
         $LFOLDERName = GetFilePath($FOLDERName)
         $Level = $Level - 1
         ScanFolder($Handle, $LFOLDERName)
      endif
      if UCASE($Item) = "#URL"
         CreateFileURL($FOLDERName)
      endif
      $EOF = ReadItem($Handle)
   loop
endfunction

;---------------------------------------------------------------
;
;---------------------------------------------------------------
function Main($OperaFile, $IEDir)
;begin
   if EXIST ($OperaFile)
      DelDir ($IEDir)
      RD $IEDir
      MD $IEDir
      $Result = RedirectOutput($IEDir+"\opera2ie.log", 1)
      $Handle = FreeFileHandle
      if Open ($Handle, $OperaFile, 2) = 0
         $NextItem = Trim(ReadLine ($Handle))
         while (@ERROR = 0) and ((Substr($NextItem,1,1) <> "#") and (Substr($NextItem,1,1) <> "-"))
            $NextItem = Trim(ReadLine ($Handle))
         loop
         ScanFolder($Handle, $IEDir)
         $Res = Close ($Handle)
      endif
      $Result = RedirectOutput("")
   endif
endfunction
